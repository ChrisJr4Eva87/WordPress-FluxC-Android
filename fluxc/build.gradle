buildscript {
    ext.kotlin_version = '1.1.2-3'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

repositories {
    jcenter()
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        versionCode 4
        versionName "0.1"
        minSdkVersion 14
        targetSdkVersion 25
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

android.buildTypes.all { buildType ->
    // Load gradle properties and add them to BuildConfig
    Properties gradleProperties = new Properties()
    File propertiesFile = file("gradle.properties")
    if (propertiesFile.exists()) {
        gradleProperties.load(new FileInputStream(propertiesFile))
    } else {
        // Load defaults
        gradleProperties.load(new FileInputStream(file("gradle.properties-example")))
    }
    gradleProperties.any { property ->
        if (property.value.equals("true") || property.value.equals("false")) {
            buildType.buildConfigField "boolean", property.key.replace("fluxc.", "").replace(".", "_").toUpperCase(),
                    "Boolean.parseBoolean(\"${property.value}\")"
        } else {
            buildType.buildConfigField "String", property.key.replace("fluxc.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    // WordPress libs
    compile ('org.wordpress:utils:1.+') {
        // Using official volley package
        exclude group: "com.mcxiaoke.volley";
    }

    // Custom WellSql version
    compile 'org.wordpress:wellsql:1.1.3'
    annotationProcessor 'org.wordpress:wellsql-processor:1.1.3'
    kapt 'org.wordpress:wellsql-processor:1.1.3'

    // FluxC annotations
    compile project(':fluxc-annotations')
    annotationProcessor project(':fluxc-processor')
    kapt project(':fluxc-processor')

    // External libs
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.google.dagger:dagger:2.0.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

version = android.defaultConfig.versionName
group = "org.wordpress"
